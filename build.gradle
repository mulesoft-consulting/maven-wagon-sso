import java.util.regex.Pattern

group 'com.brady'
version '1.0-SNAPSHOT'

configurations {
    provided
}

apply plugin: 'java'
apply plugin: 'groovy'

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Maven does not come with Groovy
    compile "org.codehaus.groovy:groovy-all:2.4.6"
    // is already built-in to Maven
    provided 'org.apache.maven.wagon:wagon-provider-api:2.12'
    // we need 4.5.3 to get Windows SPNEGO friendly auth. Maven 3.5.0 already includes base 4.5.3
    // but not the Windows pieces, so add that here
    def httpClientVersion = '4.5.3'
    compile "org.apache.httpcomponents:httpclient:${httpClientVersion}"
    compile "org.apache.httpcomponents:httpclient-win:${httpClientVersion}"
    compile group: 'net.sourceforge.htmlunit', name: 'htmlunit', version: '2.28'
    // TODO: Windows friendly slashes??
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.apache.ant:ant:1.9.7'
    testCompile 'io.vertx:vertx-web:3.5.0'
}

jar {
    from {
        // Maven comes with everything marked provided above
        configurations.compile.collect { z -> zipTree(z) }
    }
}

// TODO: Windows friendly command execution to find Maven home
def mvnVersionOutput = 'mvn --version'.execute().text
def mavenMatcher = Pattern.compile(/(?m)^Maven home: (.*)$/).matcher(mvnVersionOutput)
assert mavenMatcher.find()
def mavenHome = mavenMatcher.group(1)
println "Derived Maven home directory as '${mavenHome}'"

def jarFile = jar.outputs.files[0]
def libExt = new File(new File(mavenHome, 'lib'), 'ext')
task install(type: Copy, dependsOn: jar) {
    description 'Installs the wagon JAR into your Maven home directory'
    from jarFile
    into libExt
}

task uninstall(type: Delete) {
    description 'Uninstalls the wagon JAR from your Maven home directory'
    delete new File(libExt, jarFile.name)
}

clean {
    // we download a copy of Maven during our tests, clean it out too
    delete 'tmp'
    // IntelliJ
    delete 'out'
}
