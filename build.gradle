import java.util.regex.Pattern

group 'com.brady'
version '1.0-SNAPSHOT'

configurations {
    provided
}

apply plugin: 'java'
apply plugin: 'groovy'

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

// TODO: Windows friendly command execution
def mvnVersionOutput = 'mvn --version'.execute().text
def mavenMatcher = Pattern.compile(/(?m)^Maven home: (.*)$/).matcher(mvnVersionOutput)
assert mavenMatcher.find()
def mavenHome = mavenMatcher.group(1)
println "Derived Maven home directory as '${mavenHome}'"

dependencies {
    // Maven does not come with Groovy
    compile "org.codehaus.groovy:groovy-all:2.4.6"
    // is already built-in to Maven
    provided 'org.apache.maven.wagon:wagon-provider-api:2.12'
    // TODO: Windows friendly slashes??
    // the only way to get the shaded dependency that we must depend on, it also already includes
    // Apache's 4.5 HTTP client
    provided files("${mavenHome}/lib/wagon-http-2.12-shaded.jar")
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    from {
        // Maven comes with everything marked provided above
        configurations.compile.collect { z -> zipTree(z) }
    }
}
//println "output is ${new File(new File(buildDir.absolutePath, 'libs'), jar.outputs.files[0].name)}"
task install(type: Copy, dependsOn: jar) {
    from jar.outputs.files[0]
    into new File(new File(mavenHome, 'lib'), 'ext')
}
